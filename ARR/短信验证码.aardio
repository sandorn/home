
import win.ui;
/*DSG{{*/
var winform = win.form(text="sms-activate 短信激活工具";right=985;bottom=681;bgcolor=12632256;border="dialog frame")
winform.add(
btnCancel={cls="button";text="取消订单";left=592;top=543;right=709;bottom=574;db=1;dr=1;z=14};
btnConfirm={cls="button";text="已完成激活任务";left=668;top=621;right=792;bottom=652;db=1;dr=1;z=25};
btnCopy={cls="button";text="复制";left=593;top=621;right=659;bottom=652;db=1;dr=1;z=15};
btnCountryInfo={cls="button";text="查询可用号码数量";left=392;top=12;right=578;bottom=39;dl=1;dr=1;dt=1;z=4};
btnGetNumber={cls="button";text="购买号码并准备接收验证码";left=593;top=503;right=952;bottom=534;db=1;dr=1;z=6};
cmbService={cls="combobox";left=125;top=13;right=333;bottom=39;dl=1;dt=1;edge=1;items={};mode="dropdown";z=1};
editActivationId={cls="edit";left=349;top=542;right=584;bottom=571;db=1;dl=1;dr=1;edge=1;multiline=1;readonly=1;z=7};
editCount={cls="edit";left=824;top=466;right=950;bottom=489;db=1;dr=1;edge=1;multiline=1;z=23};
editCountryName={cls="edit";left=98;top=466;right=224;bottom=489;db=1;dl=1;edge=1;multiline=1;z=17};
editEnglishCountryName={cls="edit";left=357;top=466;right=483;bottom=489;db=1;dl=1;edge=1;multiline=1;z=19};
editPhoneNumber={cls="edit";left=349;top=582;right=584;bottom=611;db=1;dl=1;dr=1;edge=1;multiline=1;readonly=1;z=8};
editRetailPrice={cls="edit";left=593;top=466;right=719;bottom=489;db=1;dr=1;edge=1;multiline=1;z=21};
editVerificationCode={cls="edit";left=349;top=622;right=584;bottom=651;db=1;dl=1;dr=1;edge=1;multiline=1;readonly=1;z=11};
lbCountryInfo={cls="static";left=601;top=14;right=1267;bottom=42;dr=1;dt=1;transparent=1;z=5};
lbPhoneNumberTip={cls="static";left=-2;top=659;right=987;bottom=683;align="center";db=1;dl=1;dr=1;edge=1;notify=1;transparent=1;z=13};
listview={cls="listview";left=12;top=48;right=972;bottom=454;db=1;dl=1;dr=1;dt=1;edge=1;fullRow=1;msel=false;z=16};
static={cls="static";text="选择服务：";left=17;top=17;right=103;bottom=41;align="right";dl=1;dt=1;transparent=1;z=2};
static2={cls="static";text="已选择国家：";left=0;top=469;right=95;bottom=493;align="right";db=1;dl=1;transparent=1;z=3};
static3={cls="static";text="国家（英文名）：";left=236;top=469;right=351;bottom=493;align="right";db=1;dl=1;transparent=1;z=18};
static4={cls="static";text="激活 ID：";left=244;top=546;right=333;bottom=570;align="right";db=1;dl=1;transparent=1;z=9};
static5={cls="static";text="电话号码：";left=244;top=587;right=333;bottom=611;align="right";db=1;dl=1;transparent=1;z=10};
static6={cls="static";text="验证码：";left=244;top=625;right=333;bottom=649;align="right";db=1;dl=1;transparent=1;z=12};
static7={cls="static";text="号码零售价格：";left=475;top=469;right=590;bottom=493;align="right";db=1;dl=1;dr=1;transparent=1;z=20};
static8={cls="static";text="可用号码数：";left=723;top=469;right=821;bottom=493;align="right";db=1;dr=1;transparent=1;z=22};
staticCountryFlag={cls="static";left=52;top=523;right=212;bottom=643;bgcolor=16777215;db=1;dl=1;z=24}
)
/*}}*/

/*
为了简化 ChatGPT 注册流程，调用 sms-activate 官方 API 写了个注册接码的桌面工具。
接码操作更简单
直接调用 API 访问速度更快
可以根据接码价格自动排序
如果收不到码可以一键『取消订单』（自动返还费用）。
收码后会播报语音提示
*/

//创建 REST 客户端
import web.rest.smsActivate;

//请将参数中的 API key 更换为您自己的 key
var sms = web.rest.smsActivate("6394c2270e9d456f1d26e468Abf0ded7");

//搜索服务（自动补全）
import win.debounce;
winform.cmbService.onEditChange  = win.debounce(function(){ 
  var kw = winform.cmbService.text;
  
  var services = sms.findServices(kw); 
  winform.cmbService.autoComplete(services,1) //更新下拉列表  
},300)

//下拉服务列表
winform.cmbService.onDropDown = function(){ 
  var kw = winform.cmbService.text;
  var services = sms.findServices(kw); 
  winform.cmbService.autoComplete(services,1) //更新下拉列表  
}

//下拉框选择服务
winform.cmbService.onOk = function(){ 
  if(owner.selText != winform.currentServiceName){
    winform.cmbService.text = owner.selText;
    winform.btnCountryInfo.oncommand();    
  }
}

//仅英文输入
winform.cmbService.editBox.disableInputMethod();

//查询指定服务可用号码数量
winform.btnCountryInfo.oncommand = function(id,event){ 
  
  var serviceCode = web.rest.smsActivate.getServiceCodeByName( winform.cmbService.text );
  if(!serviceCode) return winform.msgboxErr("无效的服务名");
  
  winform.currentServiceName = winform.cmbService.text;
  winform.btnCountryInfo.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"}
  
  ..thread.invoke( 
    function(winform,serviceCode){
      import web.rest.smsActivate;
      var sms = web.rest.smsActivate();
      
      var data,err = sms.findCountriesByService(serviceCode); 
      if(data){
        winform.listview.onSetDataTable(data,serviceCode);
        
        var balance = sms.getBalance();
        if(balance && data){
          winform.lbCountryInfo.text = "我的余额：" + balance+ "₽"
        }
        else {
          winform.lbCountryInfo.text = "获取失败，请重试！";
        } 
        
        winform.btnCountryInfo.disabledText = null;        
      }
      else {
        winform.btnCountryInfo.disabledText = null;  
        
        winform.lbCountryInfo.text = "调用服务器 API 时遇到错误 " + (err:"");
        if(err=="BAD_KEY"){
          winform.msgboxErr("API key 错误！")
        }
      } 
        
    },winform,serviceCode);
}

//用于显示国家与地区旗帜
import web.form;
var wb = web.form(winform.staticCountryFlag);
wb.go(sms.flagUrl("China"))

//选择国家与地区
var selectCountry = function(item){
  var data = winform.listview.getItemText(item,-1);
  
  winform.editCountryName.text = data[2];
  winform.editEnglishCountryName.text = data[3];
  winform.editRetailPrice.text = data[4] + " ₽";
  winform.editCount.text = data[5];
  winform.currentCountryId = data[1];
  
  wb.go( sms.flagUrl(winform.currentCountryId) );
}

//创建列表，用于显示可用号码状态
import win.ui.grid;
var grid = win.ui.grid(winform.listview);//创建数据视图 
grid.setColumns({"ID  ","国家或地区","英文名","零售价（₽）","可用号码数"})

//在列表中选择国家与地区
grid.onSelChanged = function(selected,item,subItem,nmListView){
  if(selected){
    selectCountry(item)
  }
}

//显示国家与地区号码状态查询结果
grid.onSetDataTable = function(dataTable,serviceCode){
  grid.setColumnImage(4,0);
  grid.setTable(dataTable)
  grid.dataTable = dataTable;
  
  winform.currentServiceCode = serviceCode;
  
  if(#dataTable)selectCountry(1);
  else {
    winform.editCountryName.text = "";
    winform.editEnglishCountryName.text = "";
    winform.editRetailPrice.text = "";
    winform.editCount.text = "";
    winform.currentCountryId = null;
    wb.go("about:blank");
  }
}

//排序
grid.onSortColumn = function(column,desc){
  var name = grid.dataTable.fields[column];
  
  table.sort(grid.dataTable,desc ? (
    lambda(next) string.collate(owner[name],next[name]) > 0
  ) : (
    lambda(next) string.collate(owner[name],next[name]) < 0
  ) );   
  
  grid.setTable( grid.dataTable );
  return true; //返回 true 允许当前列排序
}

//保存配置
import fsys.config;
var config = fsys.config(io.appData("/aardio/smsActivate"));
winform.bindConfig( config.winform,{
  cmbService = "text";
} );

if(!winform.cmbService.text){
  winform.cmbService.text = "OpenAI";
}

//获取激活号码
winform.btnGetNumber.oncommand = function(id,event){
  if(!winform.currentServiceCode) return winform.msgboxErr("请先点击『查询可用号码数量』");
  if(!winform.currentCountryId) return winform.msgboxErr("请先选择『国家』");
  
  winform.editActivationId.text = "";
  winform.editPhoneNumber.text = "";
  winform.lbPhoneNumberTip.text = "";
    
  winform.btnGetNumber.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"}

  ..thread.invoke( 
    function(winform){
      import web.rest.smsActivate;
      var sms = web.rest.smsActivate();
      
      var ret,err = sms.getNumber(winform.currentServiceCode,winform.currentCountryId); 
      
      if(ret[["activationId"]]){ 
        
        sms.startActivation( ret[["activationId"]] );
        
        winform.editActivationId.text = ret[["activationId"]];
        winform.editPhoneNumber.text = ret[["phoneNumber"]];
        winform.editVerificationCode.text = "";
        
        winform.lbPhoneNumberTip.text = "已获取号码：" + ret[["phoneNumber"]]; 
        winform.btnConfirm.disabled = true;
        winform.btnCopy.disabled = true;
        
        var balance = sms.getBalance();
        if(balance){
          winform.lbCountryInfo.text = "我的余额：" + balance+ "₽"
        }
        else {
          winform.lbCountryInfo.text = "获取我的余额失败，请重试！";
        }
      } 
      else { 
        winform.msgboxErr("购买号码失败 "+(err:""))
      }
        
      winform.btnGetNumber.disabledText = null;
      
    },winform); 

}

//取消订单（收不到验证码时点这里退费）
winform.btnCancel.oncommand = function(id,event){
  var acId = winform.editActivationId.text;
  if(!#acId) return winform.msgboxErr("请指定激活 ID");
  
  winform.btnCancel.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"}
  
  thread.invoke( 
    function(winform,acId){
      import web.rest.smsActivate;
      var sms = web.rest.smsActivate();    
      
      var ok,err = sms.cancelActivation(acId);
      winform.btnCancel.disabledText = null;
      
      if(ok){
        winform.editActivationId.text = "";
        winform.editPhoneNumber.text = "";
        winform.lbPhoneNumberTip.text = "订单已取消";  
        winform.btnConfirm.disabled = true;
        
        winform.msgbox("订单已取消");
        
        var balance = sms.getBalance();
        if(balance){
          winform.lbCountryInfo.text = "我的余额：" + balance+ "₽"
        }
        else {
          winform.lbCountryInfo.text = "获取我的余额失败，请重试！";
        }
        
      } 
      else {
        winform.lbPhoneNumberTip.text = "取消订单失败：" + (err:""); 
        winform.msgboxErr(winform.lbPhoneNumberTip.text);
      }
    },winform,acId
  ) 
}

//复制
winform.btnCopy.oncommand = function(id,event){
  var vCode = winform.editVerificationCode.text;
  if(!#vCode){
    return ; 
  }
  
  import win.clip;
  win.clip.write(vCode)
}

//确认并完成激活订单
winform.btnConfirm.oncommand = function(id,event){
  var acId = winform.editActivationId.text;
  if(!#acId) return winform.msgboxErr("请指定激活 ID");
  
  winform.btnConfirm.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"}
  
  thread.invoke( 
    function(winform,acId){
      import web.rest.smsActivate;
      var sms = web.rest.smsActivate();    
      
      var ok,err = sms.confirmActivation(acId); 
      
      if(ok){ 
        winform.lbPhoneNumberTip.text = "已完成激活";    
        winform.btnConfirm.disabled = true;
      } 
      else {
        winform.lbPhoneNumberTip.text = "完成激活失败：" + (err:""); 
      }
      
      winform.btnConfirm.disabledText = null;
    },winform,acId
  )   
}

//检查是否收到验证码
thread.invoke( 
  function(winform){
    var preStatus;
    
    do{  
      import web.rest.smsActivate;
      var sms = web.rest.smsActivate();
      
      if(#winform.editVerificationCode.text){
        continue;
      }
      
      if(!#winform.editActivationId.text){
        var activations = sms.getActiveActivations();
        
        if(!(activations && #activations)){
          continue;
        }
        
        for(k,act in activations){
          if(!act.smsText){
            winform.editActivationId.text = act.activationId;
            winform.editPhoneNumber.text = act.phoneNumber;
          } 
        }
        
        if(!#winform.editActivationId.text) {
          var act = activations[1];
          winform.editActivationId.text = act.activationId;
          winform.editPhoneNumber.text = act.phoneNumber;
          
          continue;
        }
      }
        
      var vCode = sms.getSmsCode(winform.editActivationId.text);
      if(vCode){
          
        winform.editVerificationCode.text = vCode;
        
        import com.sapi.voice;//导入语音组件
        var voice = com.sapi.voice();//创建语音对象
        voice.setVoiceByLanguage(804) 
        
        for(i=1;2;1){
          voice.speakAsync("已收到验证码:" + vCode,1);//异步非阻塞朗读
          voice.waitOne();//等待朗读结束
        } 
        winform.btnConfirm.disabled = false;
      }
      
    }while(sleep(2000));
    
  },winform
) 

winform.show(); 
win.loopMessage();