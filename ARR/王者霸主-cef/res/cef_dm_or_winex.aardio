//RUNAS//王者霸业cef//DM  20210521

//调试模式
_USE_DM = 1 //大漠或是winex开关

var G_dm
if(_USE_DM) {
    import dm
    G_dm = dm()
}

if(!_STUDIO_INVOKED) {
    import xJlib.console
    xJlib.console.set(false)
    print = xJlib.console.print

    if(_USE_DM) {
        print("DmObject:", G_dm.ver())
    } else {
        print("WinEx")
    }
}

import win.ui;
/*DSG{{*/
var winform = win.form(text="王者霸业";right=1222;bottom=775)
winform.add(
btnSniff={cls="button";text="抓包";left=1045;top=716;right=1121;bottom=739;db=1;dr=1;z=57};
btnStop={cls="button";text="停止";left=1134;top=716;right=1210;bottom=739;db=1;dr=1;z=58};
buttonBack={cls="button";text='\u2190';left=478;top=4;right=526;bottom=29;disabled=1;dl=1;dt=1;z=4};
buttonForward={cls="button";text='\u2192';left=537;top=4;right=585;bottom=29;disabled=1;dl=1;dt=1;z=5};
buttonGo={cls="button";text="go";left=411;top=4;right=466;bottom=29;dl=1;dt=1;z=2};
buttonMouse={cls="button";text="模拟鼠标";left=1038;top=342;right=1115;bottom=367;dr=1;dt=1;z=7};
buttonPractice={cls="button";text="技能修炼";left=1038;top=4;right=1115;bottom=29;dr=1;dt=1;z=3};
buttonRefresh={cls="button";text="refresh";left=597;top=4;right=645;bottom=29;disabled=1;dl=1;dt=1;z=6};
buttonStop={cls="button";text="停止修炼";left=1133;top=4;right=1210;bottom=29;disabled=1;dr=1;dt=1;z=9};
buttonStopMouse={cls="button";text="停止模拟";left=1133;top=342;right=1210;bottom=367;disabled=1;dr=1;dt=1;z=29};
checkbox1={cls="checkbox";text="1键 间隔毫秒";left=1045;top=72;right=1139;bottom=94;dr=1;dt=1;z=11};
checkbox2={cls="checkbox";text="2键 间隔毫秒";left=1045;top=104;right=1139;bottom=126;dr=1;dt=1;z=12};
checkbox3={cls="checkbox";text="3键 间隔毫秒";left=1045;top=137;right=1139;bottom=159;dr=1;dt=1;z=15};
checkbox4={cls="checkbox";text="4键 间隔毫秒";left=1045;top=169;right=1139;bottom=191;dr=1;dt=1;z=17};
checkbox5={cls="checkbox";text="5键 间隔毫秒";left=1045;top=202;right=1139;bottom=224;dr=1;dt=1;z=19};
checkbox6={cls="checkbox";text="6键 间隔毫秒";left=1045;top=234;right=1139;bottom=256;dr=1;dt=1;z=21};
checkbox7={cls="checkbox";text="7键 间隔毫秒";left=1045;top=267;right=1139;bottom=289;dr=1;dt=1;z=23};
checkbox8={cls="checkbox";text="8键 间隔毫秒";left=1045;top=299;right=1139;bottom=321;dr=1;dt=1;z=25};
ck1={cls="checkbox";text="1";left=1045;top=448;right=1078;bottom=469;dr=1;dt=1;z=35};
ck2={cls="checkbox";text="2";left=1045;top=479;right=1078;bottom=500;dr=1;dt=1;z=38};
ck3={cls="checkbox";text="3";left=1045;top=509;right=1078;bottom=530;dr=1;dt=1;z=41};
ck4={cls="checkbox";text="4";left=1045;top=540;right=1078;bottom=561;dr=1;dt=1;z=44};
ck5={cls="checkbox";text="5";left=1045;top=571;right=1078;bottom=592;dr=1;dt=1;z=47};
ck6={cls="checkbox";text="6";left=1045;top=602;right=1078;bottom=623;dr=1;dt=1;z=50};
ck7={cls="checkbox";text="7";left=1045;top=632;right=1078;bottom=653;dr=1;dt=1;z=53};
ck8={cls="checkbox";text="8";left=1045;top=663;right=1078;bottom=684;dr=1;dt=1;z=56};
custBg={cls="custom";text="custBg";left=10;top=31;right=1021;bottom=766;clipch=1;db=1;dl=1;dr=1;dt=1;edge=1;z=8};
edit1={cls="edit";text="100";left=1151;top=69;right=1191;bottom=92;align="right";color=32768;dr=1;dt=1;edge=1;z=13};
edit2={cls="edit";text="100";left=1151;top=102;right=1191;bottom=123;align="right";color=32768;dr=1;dt=1;edge=1;z=14};
edit3={cls="edit";text="100";left=1151;top=135;right=1191;bottom=156;align="right";color=32768;dr=1;dt=1;edge=1;z=16};
edit4={cls="edit";text="100";left=1151;top=167;right=1191;bottom=188;align="right";color=32768;dr=1;dt=1;edge=1;z=18};
edit5={cls="edit";text="100";left=1151;top=200;right=1191;bottom=221;align="right";color=32768;dr=1;dt=1;edge=1;z=20};
edit6={cls="edit";text="100";left=1151;top=232;right=1191;bottom=253;align="right";color=32768;dr=1;dt=1;edge=1;z=22};
edit7={cls="edit";text="100";left=1151;top=265;right=1191;bottom=286;align="right";color=32768;dr=1;dt=1;edge=1;z=24};
edit8={cls="edit";text="100";left=1151;top=297;right=1191;bottom=318;align="right";color=32768;dr=1;dt=1;edge=1;z=26};
editUrl={cls="edit";text="http://wvw.9377.com/game_login.php?gid=5179&sid=113";left=10;top=4;right=401;bottom=29;dl=1;dt=1;edge=1;z=1};
editt1={cls="edit";text="4500";left=1170;top=448;right=1210;bottom=469;align="right";color=32768;dr=1;dt=1;edge=1;z=32};
editt2={cls="edit";text="4500";left=1170;top=479;right=1210;bottom=500;align="right";color=32768;dr=1;dt=1;edge=1;z=37};
editt3={cls="edit";text="4500";left=1170;top=509;right=1210;bottom=530;align="right";color=32768;dr=1;dt=1;edge=1;z=40};
editt4={cls="edit";text="4500";left=1170;top=540;right=1210;bottom=561;align="right";color=32768;dr=1;dt=1;edge=1;z=43};
editt5={cls="edit";text="4500";left=1170;top=571;right=1210;bottom=592;align="right";color=32768;dr=1;dt=1;edge=1;z=46};
editt6={cls="edit";text="4500";left=1170;top=602;right=1210;bottom=623;align="right";color=32768;dr=1;dt=1;edge=1;z=49};
editt7={cls="edit";text="4500";left=1170;top=632;right=1210;bottom=653;align="right";color=32768;dr=1;dt=1;edge=1;z=52};
editt8={cls="edit";text="4500";left=1170;top=660;right=1210;bottom=684;align="right";color=32768;dr=1;dt=1;edge=1;z=55};
editxy={cls="edit";text="0,0";left=1134;top=385;right=1211;bottom=406;align="right";color=32768;dr=1;dt=1;edge=1;z=27};
editxy1={cls="edit";text="0,0";left=1080;top=448;right=1158;bottom=469;align="right";color=32768;dr=1;dt=1;edge=1;z=31};
editxy2={cls="edit";text="0,0";left=1080;top=479;right=1158;bottom=500;align="right";color=32768;dr=1;dt=1;edge=1;z=36};
editxy3={cls="edit";text="0,0";left=1080;top=509;right=1158;bottom=530;align="right";color=32768;dr=1;dt=1;edge=1;z=39};
editxy4={cls="edit";text="0,0";left=1080;top=540;right=1158;bottom=561;align="right";color=32768;dr=1;dt=1;edge=1;z=42};
editxy5={cls="edit";text="0,0";left=1080;top=571;right=1158;bottom=592;align="right";color=32768;dr=1;dt=1;edge=1;z=45};
editxy6={cls="edit";text="0,0";left=1080;top=602;right=1158;bottom=623;align="right";color=32768;dr=1;dt=1;edge=1;z=48};
editxy7={cls="edit";text="0,0";left=1080;top=632;right=1158;bottom=653;align="right";color=32768;dr=1;dt=1;edge=1;z=51};
editxy8={cls="edit";text="0,0";left=1080;top=663;right=1158;bottom=684;align="right";color=32768;dr=1;dt=1;edge=1;z=54};
groupbox={cls="groupbox";text="要修炼的技能";left=1030;top=45;right=1211;bottom=329;dr=1;dt=1;edge=1;z=10};
plusCur={cls="plus";left=1051;top=380;right=1083;bottom=412;background="\res\1.gif";dr=1;dt=1;notify=1;repeat="center";z=30};
static={cls="static";text="坐标";left=1097;top=387;right=1125;bottom=406;dr=1;dt=1;notify=1;transparent=1;z=28};
static2={cls="static";text="坐标";left=1080;top=424;right=1117;bottom=443;dr=1;dt=1;notify=1;transparent=1;z=33};
static4={cls="static";text="间隔";left=1170;top=424;right=1209;bottom=443;dr=1;dt=1;notify=1;transparent=1;z=34}
)
/*}}*/

//将选择状态及相关信息与配置文件绑定
import config;
winform.bindConfig(config.conf, {
    edit = "text";
    radiobutton = "checked";
    checkbox = "checked";
    combobox = "selIndex";
});

winform.onClose = function(hwnd,message,wParam,lParam){
     config.conf.save()
}

for(i = 1; 8; 1) {
    winform["editxy" + i].wndproc = function(hwnd, message, wParam, lParam) {
        if(message = 0x203 /*_WM_LBUTTONDBLCLK*/ ) {
            winform["editxy" + i].text = winform.editxy.text
            //双击坐标编辑框，拷贝探测到的
        }
        //无返回值则继续调用默认回调函数
    }
}

/*抓包{{*/
import thread.event;
var eventCtrl = thread.event("wsock.sniff.给我抓");
winform.btnStop.oncommand = function(id,event){
	winform.btnStop.disabled = true;
	winform.btnStop.text = "正在停止..."
	eventCtrl.set()
}

winform.btnSniff.oncommand = function(id,event){ 
	winform.btnSniff.disabled = true;
	winform.btnStop.disabled = false;
	eventCtrl.reset();
	
	win.invoke(
		function(){
			import win;
			import wsock.sniff;
			import thread.event;
			import xJlib.console
    		xJlib.console.set(false)
    		print = xJlib.console.print
			
			var eventCtrl = thread.event("wsock.sniff.给我抓");
			print("正在抓取HTTP包,打开网页试试......") 
			
			for(sockdata in wsock.sniff() ){ 
  				if ( sockdata.ok ){
  					print(sockdata)
  					
  					if( sockdata.tcpheader ){ 
						tinfo = wsock.decodeHttpPack( sockdata.tcpdata ) 
				 		//print(tinfo)
   					} 
  				}
  				if( eventCtrl.wait(1) ) break;
			}
			
			print("title 抓包结束") 
		} 
	)
	
	winform.btnStop.text = "停止"
	winform.btnStop.disabled = true;
	winform.btnSniff.disabled = false;
}
/*}}*/

/*创建浏览器{{*/
import web.cef.browser;
web.cef.init("//cookies//", true); //(cookies地址,是否启用自带flash插件)
chorme = web.cef.browser(winform.custBg, "http://wvw.9377.com")
// winform.editUrl.text

chorme.creatBrowser();

chorme.onChangeURL = function(browserID, url) {
    //当改变网址事件,url为unicode自行转换
    winform.editUrl.text = string.fromUnicode(url, , true)
}

//当改变网址事件,title为unicode自行转换
chorme.onChangeTitle = function(browserID, title) {
    winform.text = string.fromUnicode(title, , true)
}

chorme.onChangeState = function(id, isLoading, canGoBack, canGoForward) {
    winform.buttonBack.disabled = !canGoBack;
    winform.buttonForward.disabled = !canGoForward;
    winform.buttonRefresh.disabled = isLoading;
}
//后退
winform.buttonBack.oncommand = function(id, event) {
    chorme.goBack()
}
//前进
winform.buttonForward.oncommand = function(id, event) {
    chorme.goForward()
}
//刷新
winform.buttonRefresh.oncommand = function(id, event) {
    chorme.refresh()
}
//转到
winform.buttonGo.oncommand = function(id, event) {
    chorme.go(winform.editUrl.text)
}
//地址栏响应回车键
winform.editUrl.wndproc = function(hwnd, message, wParam, lParam) {
    if(message == 0x101 /*_WM_KEYUP*/ ) {
        if(wParam == 0xD /*_VK_RETURN*/ ) {
            winform.buttonGo.oncommand() //go按钮
        }
    } //无返回值则继续调用默认回调函数
}

/*}}*/

import thread.table;
var thrdTable = thread.table("共享参数表", true /*清空*/ )
thrdTable.flag = true
thrdTable.flag_mouse = true
thrdTable.flag_huishou = true

/*线程内函数{{*/
import thread.command;
import winex.key
import winex.mouse

var listener = thread.command();

listener.print = function(...) {
    print(...)
}

listener.bind = function(hwnd) {
    if(_USE_DM) {
        listener.dm("绑定窗口",hwnd,"gdi","windows3","windows",
        "dx.public.fake.window.min|dx.public.hack.speed", 101)
        listener.dm('HackSpeed', 1.0) //加速，1.0为原速
    }
    win.setFocus(hwnd)
}

listener.key = function(hwnd, keyvalve) {
    win.setFocus(hwnd)
    
    if(_USE_DM) {
        print(48 + keyvalve)
        listener.dm("KeyPress",48 + keyvalve)
    } else {
        winex.key.send(hwnd, tostring(keyvalve))
    }
}

listener.mouse = function(hwnd, x, y) {
    win.setFocus(hwnd)
    
    if(_USE_DM) {
        listener.dm("鼠标移动单击",x, y)
    } else {
        winex.mouse.click(hwnd, tonumber(x), tonumber(y))
    }

}

listener.dm = function(func, ...) {
    if(_USE_DM) {
        G_dm[func](...)
    }
}
/*}}*/

import winex;
/*异步查找游戏窗口{{*/
winform.setTimeout(
    function() {
        _hwnd = winex.findEx(winform.custBg.hwnd,,"Chrome_RenderWidgetHostHWND","Chrome Legacy Window")
        thrdTable.hwnd = _hwnd
        listener.bind(thrdTable.hwnd)
    }, 3000 //延时
)
/*}}*/

/*鼠标拖动定位{{*/
import win.cur;
import mouse

winform.plusCur.skin(
    background = {
        active = "\res\1.gif";
    }
)
//winform.plusCur.background = "\res\1.gif"

var hCursor = win.cur.loadfile("\res\spy.cur");
winform.plusCur.onMouseDown = function(wParam, lParam) {
    win.cur.beginCur();
    winform.plusCur.background = "\res\2.gif"
    winform.plusCur.capture = true;
}

winform.plusCur.onMouseUp = function(wParam, lParam) {
    winform.plusCur.capture = false;
    win.cur.endCur();
    winform.plusCur.background = "\res\1.gif"
}

tmid = winform.setInterval(
    function(hwnd, msg, id, tick) {
        if(win.cur.beginning) {
            var x, y = mouse.getPos();
            print(2222, thrdTable.hwnd)
            x, y = win.toClient(thrdTable.hwnd, x, y);
            winform.editxy.text = ..string.format("%d,%d", x, y)
        }
    }, 700
)

/*}}*/

/*按键模拟{{*/
winform.buttonPractice.oncommand = function(id, event) {
    thread.invoke(
        function(winform, thrdTable, listener) {
            thrdTable.flag = true //设置按键模拟开始信号
            winform.buttonPractice.disabled = true
            winform.buttonStop.disabled = false
            do {
                for(index = 1; 8; 1) {
                    if(winform['checkbox' + index].checked) {
                        listener.key(thrdTable.hwnd, index)
                        sleep(tonumber(winform['edit' + index].text))
                    }

                }
            } while (thrdTable.flag);
        }, winform, thrdTable, listener
    )
}

winform.buttonStop.oncommand = function(id, event) {
    thrdTable.flag = false //设置按键模拟结束信号

    winform.buttonPractice.disabled = false
    winform.buttonStop.disabled = true
}
/*}}*/

/*鼠标模拟{{*/
winform.buttonMouse.oncommand = function() {
    thread.invoke(
        function(winform, thrdTable, listener) {
            import string
            import win
            thrdTable.flag_mouse = true //设置鼠标模拟开始信号
            win.setFocus(thrdTable.hwnd) //设置键盘输入焦点
            winform.buttonMouse.disabled = true
            winform.buttonStopMouse.disabled = false
            do {
                for(index = 1; 8; 1) {
                    if(winform['ck' + index].checked and winform["editxy" + index].text != "0,0") {
                        TMP_poin = string.split(winform["editxy" + index].text, "<,>")
                        listener.mouse(thrdTable.hwnd, TMP_poin[1], TMP_poin[2])
                        sleep(tonumber(winform['editt' + index].text))
                    }

                }

            } while (thrdTable.flag_mouse);

        }, winform, thrdTable, listener
    )
}

winform.buttonStopMouse.oncommand = function(id, event) {
    thrdTable.flag_mouse = false //设置鼠标模拟开始信号
    winform.buttonMouse.disabled = false
    winform.buttonStopMouse.disabled = true
}

/*}}*/

//给个异步延时将浏览器窗体充满容器
winform.adjust=function(cx,cy,wParm){
	chorme.setPos(0, 0, winform.custBg.width, winform.custBg.height)
}

winform.setTimeout(function() {
    winform.adjust()
}, 250)

winform.onClose = function(hwnd, message, wParam, lParam) {
    if(thrdTable.flag) {
        winform.setTimeout(function() {
            winform.buttonStop.oncommand()
        }, 50)
    }
    if(thrdTable.flag_mouse) {
        winform.setTimeout(function() {
            winform.buttonStopMouse.oncommand()
        }, 50)
    }
}
 
winform.show()

chorme.loopMessage()
